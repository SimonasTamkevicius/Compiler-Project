/*
  Created by: Fei Song (adapted by Simonas Tamkevicius)
  File Name: cm.cup
*/
   
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

    /* Assume true at the start, but set to false with parsing errors */
    public static boolean valid = true;
:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */

/* non terminal Exp program1;
non terminal ExpList program, stmt_seq;
non terminal Exp stmt, if_stmt, repeat_stmt, assign_stmt, read_stmt, write_stmt;
non terminal Exp exp; */
   
terminal BOOL, IF, ELSE, INT, RETURN, VOID, WHILE, FALSE, TRUE ;
terminal ASSIGN, EQ, LT, LE, GT, GE, NE, TILDE, OR, AND, UMINUS, COMMA, PLUS, MINUS, TIMES, OVER, LPAREN, RPAREN, SEMI, LBRACK, RBRACK, LBRACE, RBRACE;
terminal String ID, NUM;
terminal IFX;
terminal ERROR;

non terminal Exp program;

non terminal ExpList declaration_list, local_declarations, statement_list, arg_list, param_list;

non terminal Exp declaration, var_declaration, fun_declaration, fun_prototype;
non terminal Exp type_specifier, params, param;

non terminal Exp compound_stmt, statement, expression_stmt;
non terminal Exp selection_stmt, iteration_stmt, return_stmt;

non terminal Exp expression, var;
non terminal Exp call, args;

precedence nonassoc IFX;
precedence nonassoc ELSE;

precedence left OR;
precedence left AND;
precedence right TILDE;

precedence nonassoc LT, LE, GT, GE, EQ, NE;

precedence left PLUS, MINUS;
precedence left TIMES, OVER; 
precedence right UMINUS; 

/* ----------------------------Grammar Section-------------------- */
   
start with program;

program ::= declaration_list;

declaration_list ::= declaration_list declaration | declaration;

declaration ::= var_declaration | fun_declaration | fun_prototype;

var_declaration ::= type_specifier ID SEMI | type_specifier ID LBRACK NUM RBRACK SEMI;

type_specifier ::= BOOL | INT | VOID;

fun_declaration ::= type_specifier ID LPAREN params RPAREN compound_stmt;

fun_prototype ::= type_specifier ID LPAREN params RPAREN SEMI;

params ::= param_list | VOID;

param_list ::= param_list COMMA param | param;

param ::= type_specifier ID | type_specifier ID LBRACK RBRACK;

compound_stmt ::= LBRACE local_declarations statement_list RBRACE;

local_declarations ::= local_declarations var_declaration | ;

statement_list ::= statement_list statement | ;

statement ::= expression_stmt | compound_stmt | selection_stmt | iteration_stmt | return_stmt;

expression_stmt ::= expression SEMI | SEMI;

selection_stmt ::= IF LPAREN expression RPAREN statement %prec IFX | IF LPAREN expression RPAREN statement ELSE statement;

iteration_stmt ::= WHILE LPAREN expression RPAREN statement;

return_stmt ::= RETURN expression SEMI | RETURN SEMI;

expression ::= var ASSIGN expression | expression OR expression | expression AND expression
      | TILDE expression | expression LT expression | expression LE expression
      | expression GT expression | expression GE expression | expression EQ expression
      | expression NE expression | expression PLUS expression | expression MINUS expression
      | expression TIMES expression | expression OVER expression | MINUS expression %prec UMINUS
      | LPAREN expression RPAREN | var | call | NUM | TRUE | FALSE;

var ::= ID | ID LBRACK expression RBRACK;

call ::= ID LPAREN args RPAREN;

args ::= arg_list | ;

arg_list ::= arg_list COMMA expression | expression;